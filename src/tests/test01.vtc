varnishtest "Test gwist vmod"

server s1 -repeat 2 {
       rxreq
       txresp -hdr "srv: s1"
} -start

server s2 {
       rxreq
       txresp -hdr "srv: s2"
} -start


varnish v1 -vcl {
	import gwist from "${vmod_topbuild}/src/.libs/libvmod_gwist.so";

	backend default {
		.host = "${bad_ip}";
		.port = "8089";
	}

	sub vcl_recv { return (pass); }
	sub vcl_backend_fetch {
		set bereq.backend = gwist.backend(bereq.http.addr, bereq.http.port);
	}
} -start

client c1 {
	txreq -hdr "addr: ${s1_addr}" -hdr "port: ${s1_port}"
	rxresp
	expect resp.http.srv == "s1"

	txreq -hdr "addr: ${s2_addr}" -hdr "port: ${s2_port}"
	rxresp
	expect resp.http.srv == "s2"

	txreq -hdr "addr: ${s1_addr}" -hdr "port: ${s1_port}"
	rxresp
	expect resp.http.srv == "s1"
} -run
